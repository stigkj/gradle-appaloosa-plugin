apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'gitrelease'

//name = 'gradle-appaloosa-plugin'
group = 'net.nisgits.gradle'
version = this.version

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()

    compile 'org.codehaus.jackson:jackson-mapper-asl:1.8.3'
    compile 'org.apache.httpcomponents:httpclient:4.1.2'
    compile 'org.apache.httpcomponents:httpmime:4.1.2'
    compile 'commons-io:commons-io:2.0.1'

    testCompile 'com.harlap:mock-http-server:1.0.1'
    testCompile 'org.spockframework:spock-core:0.5-groovy-1.8'

    groovy 'org.codehaus.groovy:groovy-all:1.8.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
     classifier = 'javadoc'
     from javadoc.destinationDir
}

artifacts {
     archives sourcesJar
     archives javadocJar
}

if (project.hasProperty('sign')) {
    signing {
        sign configurations.archives
    }
}

defaultProject = {
    name 'Gradle Appaloosa plugin'
    // packaging 'jar' (Packaging is set later through direct modification of the xml)
    description 'The Gradle Appaloosa plugin deploys Android applications to Appaloosa (http://appaloosa-store.com).'
    url 'https://github.com/stigkj/gradle-appaloosa-plugin'
    inceptionYear '2011'

    scm {
        developerConnection 'git@github.com:stigkj/gradle-appaloosa-plugin.git'
        connection 'git://github.com/stigkj/gradle-appaloosa-plugin.git'
        url 'https://github.com/stigkj/gradle-appaloosa-plugin'
    }

    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'stigkj'
            name 'Stig Kleppe-Jørgensen'
            email 'from.gradle-appaloosa@nisgits.net'
            roles {
                role 'Developer'
            }
            timezone '+1'
        }
        developer {
            id 'stianh'
            name 'Stian Hegglund'
            email 'stian.hegglund@gmail.com'
            roles {
                role 'Developer'
            }
            timezone '+1'
        }
    }
}

fixedProject = {
    project defaultProject
    withXml {
        def project = it.asNode()
        project.version[0] + {
            packaging 'jar'
        }
    }
}

install.repositories.mavenInstaller {
    pom fixedProject

    if (project.hasProperty('sign')) {
        beforeDeployment { deployment ->
            signPom(deployment)
        }
    }
}

uploadArchives.repositories.mavenDeployer {
    pom fixedProject
    uniqueVersion = false

    if (project.hasProperty('sign')) {
        beforeDeployment { deployment ->
            signPom(deployment)
        }
    }

    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: sonatypeUsername, password: sonatypePassword)
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'no.entitas.gradle:gradle-release-plugin:1.5'
    }
}